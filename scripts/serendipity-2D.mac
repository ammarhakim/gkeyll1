/** Serendipity basis functions in 2D */
kill(all);

/** Ordinates */
ords : [[-1,-1], [1,-1], [1,1], [-1,1]];
/** Basis function of order 2 */
N2 : 1/4*[
 (1+ords[1][1]*x)*(1+ords[1][2]*y), 
 (1+ords[2][1]*x)*(1+ords[2][2]*y), 
 (1+ords[3][1]*x)*(1+ords[3][2]*y), 
 (1+ords[4][1]*x)*(1+ords[4][2]*y)];

/** Compute mass matrix */
N2_mass : (mass : zeromatrix(4,4), for i from 1 thru 4
	    do (for j from 1 thru 4
	      do (mass[i,j] : integrate(integrate(N2[i]*N2[j], x, -1,1), y, -1, 1))),
	        mass);

/** Compute stiffness matrix */
N2_stiff : (stiff : zeromatrix(4,4), for i from 1 thru 4
	    do (for j from 1 thru 4
	      do (stiff[i,j] : integrate(integrate(
                diff(N2[i],x)*diff(N2[j],x) + diff(N2[i],y)*diff(N2[j],y),
                x, -1,1), y, -1,1))),
	        stiff);

/** Ordinates */
ords : [[-1,-1], [1,-1], [1,1], [-1,1], [0,-1], [1,0], [0,1], [-1,0]];
/** Basis functions of order 3 */
N3 : [
  1/4*(1+ords[1][1]*x)*(1+ords[1][2]*y)*(-1+ords[1][1]*x+ords[1][2]*y),
  1/4*(1+ords[2][1]*x)*(1+ords[2][2]*y)*(-1+ords[2][1]*x+ords[2][2]*y),
  1/4*(1+ords[3][1]*x)*(1+ords[3][2]*y)*(-1+ords[3][1]*x+ords[3][2]*y),
  1/4*(1+ords[4][1]*x)*(1+ords[4][2]*y)*(-1+ords[4][1]*x+ords[4][2]*y),
  1/2*(1-x^2)*(1+ords[5][2]*y),
  1/2*(1-y^2)*(1+ords[6][1]*x),
  1/2*(1-x^2)*(1+ords[7][2]*y),
  1/2*(1-y^2)*(1+ords[8][1]*x)];

/** Compute mass matrix */
N3_mass : (mass : zeromatrix(8,8), for i from 1 thru 8
	    do (for j from 1 thru 8
	      do (mass[i,j] : integrate(integrate(N3[i]*N3[j], x, -1,1), y, -1, 1))),
	        mass);

/** Compute stiffness matrix */
N3_stiff : (stiff : zeromatrix(8,8), for i from 1 thru 8
	    do (for j from 1 thru 8
	      do (stiff[i,j] : integrate(integrate(
                diff(N3[i],x)*diff(N3[j],x) + diff(N3[i],y)*diff(N3[j],y),
                x, -1,1), y, -1, 1))),
	        stiff);
