## -*- python -*-
##
# Build code and create library
#
# Copyright &copy; 2008-2009, Ammar Hakim. Released under Eclipse
# Licence version 1.0.
##

Import('env')
Import('parenv')

incs = [
    '.',
    '#etc',
    '#etc/loki/include',
    ]

# make a local copy of the build environment
myenv = env.Clone()
myenv.Append(CPPPATH = incs)

myparenv = parenv.Clone()
myparenv.Append(CPPPATH = incs)

# name of library built with code in this file
libname = 'lclib'

# lexical analysis code for WarpX input files
# This is no longer being used: Wed Dec 20 19:31:43 UTC 2006 (Ammar)
#wxlexer = myenv.CFile(target = 'wxlexer.cc', source = 'wxlexer.l')

# source files in lib
sources = [
    'lcexcept.cc',
    'lcexprparser.cc',
    'lcfilehandler.cc',
    'lchdf5io.cc',
    'lchdf5iotmpl.cc',
    'lciobase.cc',
    'lckeyval.cc',
    'lckeyvaltree.cc',
    'lckeyvaltreelexer.cc',
    'lckeyvaltreeparser.cc',
    'lclogger.cc',
    'lclogrecordhandler.cc',
    'lclogstream.cc',
    'lclogstreamstrm.cc',
    'lcstreamhandler.cc',
    ]

# mpi sources if compiling with parallel
if myenv['parallel']:
    mpi_sources = [
        'lcmpimsgop.cc',
        'lcparmpimsg.cc',
        'lcparmpitmpl.cc',
        ]
else:
    mpi_sources = [ 
        'lcparselfmsg.cc',
        'lcparselftmpl.cc',
        ]

##
# compile sources and add them to list of objects
##
objs = []
for s in sources:
    o = myenv.Object(s)
    objs.append(o)

for s in mpi_sources:
    o = myparenv.Object(s)
    objs.append(o)    
    
##
# Archive into library
##
Library(libname, objs)
